// Simple Stack Analysis
//
// The goal of stack analysis is going to be eventually to recover the majority
// of stack variables, and stack parameters.
//
// 1. Stack Pointer Tracking: this is used for recovering which registers, at
//    which program points, are referring to what part of the stack. This is
//    useful as in order to infer the location of relative stack accesses we
//    need to know whether a register is referring to stack or not, and if so
//    which part of stack. This works by first labeling the implicitly passed
//    stack registers at depth 0, and then forward-propogating stack information
//    to each instruction, taking into account stack pointer movements or
//    manipulations
// 2. Stack Access Analysis: once we know which registers contain which stack
//    depths, we can analyze memory accesses refer to which stack slots. This
//    is complicated by accesses to non-trivially-analyzed offsets (i.e. stack
//    allocated arrays). We can lean on ddisasm's value-analysis which could
//    provide us a rough upper bound on an access from its depth-limited
//    analysis, however this would probably in most cases be an unsafe
//    under-approximation.
// 3. Stack Aliasing Analysis: once we know which instructions accesses which
//    piece of stack memory, we can build an aliasing analysis. For simple
//    variables on the stack this would be trivial as this just determines
//    whether theres an overlap of (offset, offset+size) for each write. For
//    stack writes/reads that use complex accesses (i.e. a loop loading or
//    storing to stack-allocated arrays) we can consider the write to access
//    memory from (offset, offset+size*max_stride).
// 4. Stack Liveness Analysis: once we know which stack accesses can potentially
//    alias eachother, we can start making determinations about the liveness of
//    a given stack variable at any given program point with some limited
//    context sensitivity.
// 5. Stack Variable Allocation: once we have the live-range of a given stack
//    variable we can assign a unique variable identifier which to each live
//    range. These identifiers can be then used as the type-variable for
//    constraints to derive better stack types.

// An edge in the intra-procedural graph of instructions, including the first
// instruction, which has no previous instruction (and thus serves as the
// inital point for stack)
.decl stack_edge(PrevEA:address, EA:address, HasPrev:unsigned)
stack_edge(PrevEA, EA, HasPrev) :-
    next_instruction(PrevEA, EA),
    HasPrev = 1
    ;
    function_inference.function_entry_name(EA, _),
    PrevEA = 0,
    HasPrev = 0.

.decl stack_pointer_tracking(EA:address, reg:register, depth:number)
.output stack_pointer_tracking

// Handle a push or pop to the stack
stack_pointer_tracking(EA, Reg, Depth) :-
    stack_edge(PrevEA, EA, HasPrev),
    (
        push(EA),
        Mult=-1
        ;
        pop(EA),
        Mult=1
    ),
    arch.stack_pointer(Reg),
    (
        HasPrev = 1,
        stack_pointer_tracking(PrevEA, Reg, PrevDepth)
        ;
        HasPrev = 0,
        PrevDepth = 0
    ),
    arch.pointer_size(Size),
    Depth = PrevDepth + (Mult * as(Size, number)).

// Handle computing a new stack pointer
stack_pointer_tracking(EA, Reg, Depth) :-
    stack_edge(PrevEA, EA, HasPrev),
    // If we're not pushing and popping...
    !pop(EA),
    !push(EA),

    // ... and we compute Reg from SrcReg...
    explicit_writes_register(EA, Reg),
    (
        arch.reg_arithmetic_operation(EA, Reg, SrcReg, 1, Delta)
        ;
        move_instruction(EA),
        Delta=0
    ),
    (
        // ... and SrcReg is a stack reg from previous instruction ...
        HasPrev=1,
        register_access(EA, "Reads", SrcReg),
        stack_pointer_tracking(PrevEA, SrcReg, PrevDepth)
        ;
        // ... and SrcReg is the implicitly passed stack pointer ...
        HasPrev=0,
        (arch.stack_pointer(SrcReg) ; arch.frame_pointer(SrcReg)),
        PrevDepth=0
    ),

    // ... update stack depth
    Depth=PrevDepth+Delta.

// Handle normal propogations
stack_pointer_tracking(EA, Reg, Depth) :-
    stack_edge(PrevEA, EA, HasPrev),
    (
        // Let stack pointers in PUSH/POP be handled by their handler
        (pop(EA) ; push(EA)),
        !arch.stack_pointer(Reg)
        ;
        // If we're not pushing and popping...
        !pop(EA),
        !push(EA)
    ),
    (
        // ... and if we don't write at all ...
        !register_access(EA, "Writes", _)
        ;
        // ... and if we do write...
        register_access(EA, "Writes", WrittenReg),
        (
            // ... to something thats not frame/stack ...
            HasPrev = 1,
            stack_pointer_tracking(PrevEA, PrevReg, Depth),
            PrevReg != WrittenReg
            ;
            // ... to something thats not the default initialized stack ...
            HasPrev = 0,
            !arch.frame_pointer(WrittenReg),
            !arch.stack_pointer(WrittenReg)
        )
    ),
    (
        // ... do normal propogation
        HasPrev = 1,
        stack_pointer_tracking(PrevEA, Reg, Depth)
        ;
        // ... initialize the frame/stack pointer to Depth = 0
        HasPrev = 0,
        (
            arch.frame_pointer(Reg)
            ;
            arch.stack_pointer(Reg)
        ),
        reaches_without_write(EA, Reg),
        Depth = 0
    ).
